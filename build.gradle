plugins {
    id 'java'
    id 'idea'
    id 'nebula.provided-base' version '3.1.0'
}

group 'ru.endlesscode'
version '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = 1.7

ext {
    apiVersion = '0.1.3'
    bukkitVersion = '1.9.4'
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name 'jitpack-repo'
        url 'https://jitpack.io'
    }
}

configurations {
    include
}

sourceSets {
    main {
        compileClasspath += configurations.include
        runtimeClasspath += configurations.include
    }

    test {
        compileClasspath += configurations.include
        runtimeClasspath += configurations.include
    }
}

dependencies {
    include group: 'ru.endlesscode', name: 'MimicAPI', version: "$apiVersion"
    provided group: 'org.bukkit', name: 'bukkit', version: "$bukkitVersion-R0.1-SNAPSHOT"
    provided group: 'org.jetbrains', name: 'annotations', version: '15.0'
}

jar {
    from configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
    from 'LICENSE'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        filter { String line ->
            line.replace '${VERSION}', "$version"
        }
    }
}