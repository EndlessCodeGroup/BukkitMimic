plugins {
    id 'java'
    id 'idea'
    id 'nebula.provided-base' version '3.1.0'
}

apply from: 'jacoco.gradle'

group 'ru.endlesscode'
version '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = 1.7

javadoc {
    options.tags = ['apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:', 'implNote:a:Implementation Note:']
}

ext {
    bukkitVersion = '1.9.4'
    mockitoVersion = '2.7.22'
    powermockVersion = '1.7.0RC4'
    apiVersion = 'b64f62ccd1'
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name 'jitpack-repo'
        url 'https://jitpack.io'
    }
}

configurations {
    include
}

dependencies {
    compile group: 'ru.endlesscode', name: 'MimicAPI', version: "$apiVersion"
    include group: 'ru.endlesscode', name: 'MimicAPI', version: "$apiVersion"
    provided group: 'org.bukkit', name: 'bukkit', version: "$bukkitVersion-R0.1-SNAPSHOT"
    provided group: 'org.jetbrains', name: 'annotations', version: '15.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: "$mockitoVersion"
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: "$powermockVersion"
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: "$powermockVersion"
}

jar {
    from configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
    from 'LICENSE'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        filter { String line ->
            line.replace '${VERSION}', "$version"
        }
    }
}